plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.50" apply false
    id "org.jetbrains.kotlin.kapt" version "1.2.50" apply false
    id "org.jetbrains.kotlin.plugin.allopen" version "1.2.50" apply false
    id "org.jetbrains.kotlin.plugin.jpa" version "1.2.50" apply false
    id "org.jetbrains.kotlin.plugin.spring" version "1.2.50" apply false
    id "org.springframework.boot" version "2.0.4.RELEASE" apply false
}

group 'net.animeta'
version '1.0.0'

allprojects {
    group 'net.animeta'
    version '1.0.0'
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'kotlin-kapt'
    apply plugin: 'kotlin-allopen'
    apply plugin: 'kotlin-jpa'
    apply plugin: 'kotlin-spring'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
        maven {
            url 'https://repo.spring.io/libs-milestone'
        }
    }

    dependencies {
        compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
        compile("org.jetbrains.kotlin:kotlin-reflect")
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }

    allOpen {
        annotation 'javax.persistence.Entity'
        annotation 'javax.persistence.Embeddable'
    }
}
